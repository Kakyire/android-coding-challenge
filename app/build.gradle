apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: "kotlin-kapt"


android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.syftapp.codetest"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "API_ROOT", "\"https://jsonplaceholder.typicode.com\""
    }
    buildFeatures {
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

def androidx_version = '1.3.0'
def room_version = '2.3.0'
def retrofit_version = '2.9.0'
def koin_version = "3.1.2"


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.7.0-alpha01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.appcompat:appcompat:$androidx_version"
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "io.insert-koin:koin-android:$koin_version"
    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation 'com.facebook.stetho:stetho:1.6.0'

    kapt "androidx.room:room-compiler:$room_version"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.assertj:assertj-core:3.20.2'
    testImplementation 'io.mockk:mockk:1.12.0'
}

task deployDebug(type: Exec, dependsOn: 'installDebug') {
    def rootDir = project.rootDir
    def localProperties = new File(rootDir, "local.properties")
    if (localProperties.exists()) {
        Properties properties = new Properties()
        localProperties.withInputStream {
            inputStream -> properties.load(inputStream)
        }
        def sdkDir = properties.getProperty('sdk.dir')
        def adb = "$sdkDir/platform-tools/adb"
        commandLine "$adb", 'shell', 'am', 'start', '-n', 'com.syftapp.codetest/com.syftapp.codetest.posts.PostsActivity'
    }
}